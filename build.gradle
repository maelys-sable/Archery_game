plugins {
    id 'application'
    id "org.openjfx.javafxplugin" version "0.0.14"
    id 'java'
    id 'jacoco'
    id 'org.beryx.jlink' version '2.26.0'
}

version '0.1'

[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

application {
    mainModule.set('fr.ensicaen.ecole.archery')
    mainClass = 'fr.ensicaen.ecole.archery.Main'
}

repositories {
    mavenCentral()
}

javafx {
    version = "21"
    modules = [ 'javafx.controls', 'javafx.fxml', 'javafx.graphics' ] 
}

dependencies {
    implementation 'org.openjfx:javafx-controls:21'
    implementation 'org.openjfx:javafx-fxml:21'
    implementation 'org.openjfx:javafx-graphics:21'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.+'
    testImplementation 'org.mockito:mockito-core:4.+'
}

tasks.register('copyLibFolder', Copy) {
    description 'Copies the lib folder.'
    from('lib')
    into("$buildDir/classes")
}

compileJava.dependsOn copyLibFolder

applicationDistribution.from(['LICENSE', 'README.md']) {
    into ''
}

test {
    useJUnitPlatform()
}

jacoco {
    toolVersion = "0.8.11"
    reportsDirectory = layout.buildDirectory.dir('customJacocoReportDir')
}

jacocoTestReport {
    reports {
        xml.required = false
        csv.required = false
        html.outputLocation = layout.buildDirectory.dir('jacocoHtml')
    }
}

sourceSets {
    main {
        resources {
            srcDir 'src/main/'
        }
    }
}

jlink {
    jpackage {
        installerType = 'deb'
    }
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages', '--bind-services']
    launcher {
        name = 'Archery'
    }
}
